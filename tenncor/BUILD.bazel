licenses(["notice"])

load("//third_party:tools/pybind.bzl", "pybind_py_library")

package(
    default_visibility = ["//visibility:public"],
)

filegroup(
    name = "srcs",
    srcs = [
        ":tenncor_hdrs",
        ":tenncor_srcs",
        ":pybind_hdrs",
        ":pybind_srcs",
        ":ctest_srcs",
        ":ptest_srcs",
        ":benchmark_srcs",
        "//tenncor/eteq:srcs",
        "//tenncor/hone:srcs",
        "//tenncor/serial:srcs",
        "//tenncor/trainer:srcs",
        "//tenncor/distr:srcs",
        "//tenncor/layr:srcs",
        "//tenncor/pyutils:srcs",
        "BUILD.bazel",
    ],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "benchmark_srcs",
    srcs = glob(["bm/*.cpp"]),
)

filegroup(
    name = "tenncor_hdrs",
    srcs = glob(["*.hpp"])
)

filegroup(
    name = "tenncor_srcs",
    srcs = glob(["src/*.cpp"])
)

filegroup(
    name = "pybind_hdrs",
    srcs = glob(["python/*.hpp"]),
)

filegroup(
    name = "pybind_srcs",
    srcs = glob(["python/*.cpp"]),
)

filegroup(
    name = "ctest_srcs",
    srcs = glob(["test/*.cpp"]),
)

filegroup(
    name = "ptest_srcs",
    srcs = ["test/ptest.py"],
)

######### LIBRARY #########

GEN_CMD_FMT = """$(location //tools/egen:egen) --plugins \
plugins.apis:APIsPlugin \
plugins.pyapis:PyAPIsPlugin \
--out $(@D)/generated \
--strip_prefix=$(@D)/.. \
--ext_path=`dirname $$(echo '$(locations //cfg:eteq_classes)' | awk '{{print $$1}}')` \
--cfgs $(locations //cfg:{})"""

GEN_HDRS = [
    "generated/api.hpp",
    "generated/pyapi.hpp",
]

genrule(
    name = "generated_tenncor",
    srcs = select({
        "//conditions:default": ["//cfg:eteq_min"],
        "//internal/eigen:fast_build": ["//cfg:eteq_min"],
        "//internal/eigen:full_build": ["//cfg:eteq"],
    }) + ["//cfg:eteq_classes"],
    outs = GEN_HDRS + [
        "generated/api.cpp",
        "generated/pyapi_tenncor.cpp",
    ],
    tools = ["//tools/egen:egen"],
    cmd = select({
        "//conditions:default": GEN_CMD_FMT.format("eteq_min"),
        "//internal/eigen:fast_build": GEN_CMD_FMT.format("eteq_min"),
        "//internal/eigen:full_build": GEN_CMD_FMT.format("eteq"),
    })
)

cc_library(
    name = "tenncor",
    hdrs = [
        ":tenncor_hdrs",
    ] + [
        ":"+hdr for hdr in GEN_HDRS
    ],
    srcs = [
        ":generated/api.cpp",
        ":tenncor_srcs",
    ],
    copts = ["-std=c++17"],
    include_prefix = "tenncor",
    includes = ["."],
    deps = [
        "//tenncor/hone:hone",
        "//tenncor/eteq/opsvc:opsvc",
        "//tenncor/serial/oxsvc:oxsvc",
        "//tenncor/trainer:trainer",
    ],
)

pybind_py_library(
    name = "tenncor_py",
    cc_srcs = [
        ":pybind_hdrs",
        ":pybind_srcs",
        ":generated/pyapi_tenncor.cpp",
    ],
    cc_deps = [
        ":tenncor",
        "//dbg/print/printsvc:printsvc",
        "//tenncor/pyutils:pyutils",
    ],
    copts = [
        "-std=c++17",
        "-Itenncor",
    ],
    py_imports = ["."]
)

######### BENCHMARK #########

cc_binary(
    name = "benchmark",
    srcs = [":benchmark_srcs"],
    deps = [
        ":tenncor",
        "@com_github_google_benchmark//:benchmark",
    ],
    copts = ["-std=c++17"],
    data = ["//cfg:optimizations"],
)

######### TEST #########

cc_test(
    name = "ctest",
    srcs = [":ctest_srcs"],
    copts = ["-std=c++17"],
    deps = [
        "//internal/utils/coord:coord",
        "//tenncor:tenncor",
        "//testutil:tutil",
        "//dbg/print/printsvc:printsvc",
        "@com_github_mingkaic_cppkg//diff:diff",
        "@com_github_mingkaic_cppkg//exam:exam",
    ],
    linkstatic = True,
    data = [
        "//:test_models",
        "//cfg:optimizations",
    ],
)

py_test(
    name = "ptest",
    srcs = [":ptest_srcs"],
    deps = [
        "//tenncor:tenncor_py",
        "//testutil:pyunit_util",
    ],
    data = [
        "//:test_models",
        "//testutil:ead_testcase_template",
    ],
)
