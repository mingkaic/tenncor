licenses(["notice"])

filegroup(
    name = "srcs",
    srcs = [
        ":eigen_hdrs",
        ":eigen_srcs",
        ":test_srcs",
        "BUILD.bazel",
    ],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "eigen_hdrs",
    srcs = glob(["*.hpp"]),
)

filegroup(
    name = "eigen_srcs",
    srcs = glob(["src/*.cpp"]),
)

filegroup(
    name = "test_srcs",
    srcs = glob([
        "test/*.hpp",
        "test/*.cpp",
    ]),
)

######### LIBRARY #########

config_setting(
    name = "fast_build",
    values = {
        "define": "ETEQ_CFG=MIN",
    },
    visibility = ["//visibility:public"],
)

config_setting(
    name = "full_build",
    values = {
        "define": "ETEQ_CFG=FULL",
    },
    visibility = ["//visibility:public"],
)

GEN_CMD_FMT = """$(location //egen:egen) --plugins \
plugins.dtypes:DTypesPlugin \
plugins.opcodes:OpcodesPlugin \
--out $(@D)/generated \
--strip_prefix=$$(dirname $(@D)) \
--cfgs $(locations //cfg:{})"""

GEN_HDRS = [
    "generated/opcode.hpp",
    "generated/dtype.hpp",
]

GEN_SRCS = [
    "generated/opcode.cpp",
    "generated/dtype.cpp",
]

genrule(
    name = "generated_eigen",
    srcs = select({
        "//conditions:default": ["//cfg:eteq_min"],
        ":fast_build": ["//cfg:eteq_min"],
        ":full_build": ["//cfg:eteq"]
    }),
    outs = GEN_HDRS + GEN_SRCS,
    tools = ["//egen:egen"],
    cmd = select({
        "//conditions:default": GEN_CMD_FMT.format("eteq_min"),
        ":fast_build": GEN_CMD_FMT.format("eteq_min"),
        ":full_build": GEN_CMD_FMT.format("eteq"),
    })
)

cc_library(
    name = "eigen",
    hdrs = [":eigen_hdrs"] + [
        ':'+hdr for hdr in GEN_HDRS
    ],
    srcs = [":eigen_srcs"] + [
        ':'+src for src in GEN_SRCS
    ],
    copts = ["-std=c++17"],
    deps = [
        "//teq:teq",
        "@com_github_eigenteam_eigen//:eigen",
    ],
    visibility = ["//visibility:public"],
)

######### TEST #########

cc_test(
    name = "test",
    srcs = [":test_srcs"],
    copts = ["-std=c++17"],
    deps = [
        ":eigen",
        "//teq:mock_teq",
        "//dbg:print",
        "@gtest//:gtest",
        "@com_github_mingkaic_cppkg//diff:diff",
        "@com_github_mingkaic_cppkg//exam:exam",
    ],
    linkstatic = True,
)
